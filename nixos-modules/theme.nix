{ inputs, pkgs, lib, config, isLinux, isServer, ... }:
with lib;
let inherit (config) theme;
in {
  imports = [

    inputs.catppuccin.homeManagerModules.catppuccin
    inputs.tokyonight.homeManagerModules.default
  ];
  options = {
    theme = mkOption {
      type = types.enum [ "catppuccin" "tokyonight" ];
      description = "Which theme to use.";
    };
  };
  config = mkMerge [
    (mkIf (theme == "catppuccin") {
      home.sessionVariables.COLORSCHEME = "catppuccin";
      # enable globally for all supported programs
      catppuccin = {
        enable = true;
        flavor = "mocha";
      };
      # My Neovim Lua is not generated by Nix
      programs.neovim.catppuccin.enable = false;
    })

    (mkIf (theme == "tokyonight") {
      home.sessionVariables.COLORSCHEME = "tokyonight";
      # enable globally for all supported programs
      tokyonight = {
        enable = true;
        style = "night";
      };
      programs.btop.settings = {
        color_theme = "tokyonight-night";
        theme_background = false;
      };
      # handle GTK themeing
      gtk = {
        enable = isLinux && !isServer;
        theme = {
          package = pkgs.tokyonight-gtk-theme.override {
            # macos style window buttons
            tweakVariants = [ "macos" ];
          };
          name = "Tokyonight-Dark";
        };
      };

      dconf.settings."org/gnome/shell/extensions/user-theme".name =
        "Tokyonight-Dark";

      xdg.configFile = { } // lib.optionalAttrs isLinux {
        "gtk-4.0/assets".source =
          "${config.gtk.theme.package}/share/themes/${config.gtk.theme.name}/gtk-4.0/assets";
        "gtk-4.0/gtk.css".source =
          "${config.gtk.theme.package}/share/themes/${config.gtk.theme.name}/gtk-4.0/gtk.css";
        "gtk-4.0/gtk-dark.css".source =
          "${config.gtk.theme.package}/share/themes/${config.gtk.theme.name}/gtk-4.0/gtk-dark.css";
      };
      programs = {
        # My Neovim Lua is not generated by Nix
        neovim.tokyonight.enable = false;
      };
    })
  ];
}

